#! /bin/csh -f
# t2z3_script
# 10.25.96 by Jose' Ma. Maisog, M.D.
# Laboratory of Psychology and Psychopathology,
# National Institute of Mental Health

# Shell script to convert t-test maps to Z-score maps

# C executables called: get_dims_from, t2z3, makeheader4, maxfp, minfp

# QUERY USER FOR IMAGE DIRECTORY
scroll 10
echo  ""
echo '******************************************************'
echo  ""
echo  Shell script to convert a t-test map to a Z-score map.
echo ""
echo This script will convert floating point t-test maps to floating point
echo Z-score maps.  The t-test maps must be floating point ANALYZE 
echo files and must have appropriate ANALYZE headers.  You must have read/write
echo access to the directory in which the files are kept.
echo ""
echo References:
echo ""
echo Federighi, E.T., \"Extended tables of the percentage points
echo of Student\'s t-distribution\" J. Am. Stat. Assoc.,
echo 54, 683-688 \(1959\).
echo ""

# Query user for directory
echo ""
echo -n "Please enter directory in which floating point t-test maps are kept: "
set dir = $<
while (! -d $dir)
    echo Directory $dir does not exist.
    echo ""
    echo -n "Please enter directory in which floating point t-test maps are kept: "
    set dir = $<
end
cd $dir
touch fidap.log
echo '******************************************************' >> fidap.log
echo `date` " : " $user " : " t2z3_script >> fidap.log

# MAKE LIST OF FILES TO BE CONVERTED TO Z-SCORES.
echo ""
echo Here are the ANALYZE files found in this directory:
ls *.img
echo ""
echo Need root name of maps to be converted.  This is the left-most
echo string of characters in the image filenames which will match all
echo of the maps you wish to slice, and ONLY those maps.
echo ""
echo -n "Type in root name of maps to be converted: "
set rootname = $<
echo ""
set list = $rootname*.img
echo These t-test maps are going to be converted to Z-score maps:
echo $list
echo ""
echo They are assumed to already have ANALYZE headers.

# GET DEGREES OF FREEDOM FOR EACH t-TEST MAP
echo ""
echo You will now have to type in the degrees of freedom for each
echo t-test map.  Degrees of freedom may need to be corrected
echo for spatial and/or temporal autocorrelations.
echo ""
set df
set img_no = 1
while ($img_no <= $#list)
    echo -n "Degrees of freedom for $list[$img_no]? "
    set df = ($df $<)
    echo ""
    @ img_no++
end

# INFORMATION ABOUT OUTPUT MAP FILES
echo ""
echo t-test maps will be converted to floating point Z-score maps with
echo naming convention
echo ""
echo "    Z<filename>"
echo ""
echo "where <filename> is the name of the original t-test map."
echo ""

# CHECK WITH USER IF IT'S OKAY TO GO ON
echo ""
echo ""
echo -n "Continue <y/n>? "
set choice = $<
if ($choice != "y") then
    echo QUITTING SHELL SCRIPT....
    sleep 2
    exit
endif

# LOOP OVER ALL T-TEST MAPS
set img_no = 1
while ($img_no <= $#list)
    echo ""
    echo Converting $list[$img_no] to Z$list[$img_no]...

    # CALL t2z3
    t2z3 $list[$img_no] $df[$img_no] Z$list[$img_no]

    # MAKE HEADER FOR Z-SCORE MAP
    set max = `maxfp Z$list[$img_no]`
    set min = `minfp Z$list[$img_no]`
    set dims = `get_dims_from $list[$img_no]:r.hdr`
    makeheader4 Z$list[$img_no]:r.hdr $dims 1 32 $max $min $max $min 16

    # CONVERT TO BYTE FORMAT FOR VIEWING
    fp2byte_script Z$list[$img_no]

    @ img_no++
end

